#NOP Time Lock handler
#NOP By Balthus.

#ALIAS {.Lock} 
{
    #IF {"%0" == ""}
    {
        #LOCAL {_l} {};
        #LOCAL {_lockTable} {};
        #ECHO {Locks Active:};
        #ECHO {=============};
        #FOREACH {*_lock[]} {_l}
        {
            #LOCAL {_lockTable[$_l][Time]} {@fFormatTimestamp{$_lock[$_l]}};
        };
        #LOCAL {_lockTable} {@fTable{$_lockTable}};
        #LIST {_lockTable} COLLAPSE {\n};
        #ECHO {$_lockTable};
    };
    #ELSE
    {
        #FORMAT {_lock[%1]} {%T};
        #IF {@fIsNumber{%2}}
        {
            #MATH {_lock[%1]} {$_lock[%1] + %2};
        };
        #ELSE 
        {
            #NOP Default lock for 1 hour if not specified;
            #MATH {_lock[%1]} {$_lock[%1] + 3600};
        };
        .file write {$path[profile]/lock.tin};        
        event_raise {e_lock_added} {%0};
    };
};

#ALIAS {.Unlock}
{
    #IF {"%0" == ""}
    {
        .Lock;
    };
    #ELSE
    {
        #LOCAL {_lock_record} {};
        #FOREACH {*_lock[%1]} {_lock_record}
        {
            event_raise {e_lock_removed} {$_lock_record};
        };
        #UNVAR {_lock[%1]};
        .file write {$path[profile]/lock.tin};
    };
};

#FUNCTION {fLocked}
{
    #LOCAL {_now} {};
    #FORMAT {_now} {%T};
    #LOCAL {_l} {};
    #FOREACH {*_lock[]} {_l}
    {
        #IF {$_lock[$_l] < $_now}
        {
            .Unlock {$_l};       
        };
    };
    #IF {&_lock[%0] > 0}
    {
        #RETURN 1;
    };
    #RETURN 0;
};

#IF {&{_lock} == 0}
{
    #CLASS {$path[profile]/lock.tin} ASSIGN 
    {
        #VAR {_lock} {};
    };
};