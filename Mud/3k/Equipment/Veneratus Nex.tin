#NOP Vis:[400/400] Proximus:[1%]

#NOP -*-*-*-*-*-*-*-* Veneratus Nex *-*-*-*-*-*-*-*-*-
#NOP Actus:   [6]         Actus Proximus: [88.3719%]
#NOP Vis: [ 403/900 ]     Loricatus:      [3]
#NOP -------------------------------------------------
#NOP Tutaminis [Desino]   Vivifico   [Desino]
#NOP Victus    [Desino]   Praesentia [Desino]
#NOP Incursus  [Ineo]
#NOP -*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-

#ALIAS {agnosco}
{
    #SUBSTITUTE {Actus} {Level};
    #SUBSTITUTE {Proximus} {Progress};
    #SUBSTITUTE {Vis} {Power};
    #SUBSTITUTE {Loricatus} { Items};
    #SUBSTITUTE {Desino} {Off};
    #SUBSTITUTE {Ineo} {On };
    #SUBSTITUTE {Tutaminis} {Tutaminis (Defence) };
    #SUBSTITUTE {Vivifico} {Vivifico (Light)   };
    #SUBSTITUTE {Victus} {Victus (HP Regen)};
    #SUBSTITUTE {Praesentia} {Praesentia (SP Regen)};
    #SUBSTITUTE {Incursus} {Incursus (Attack)  };
    #ACTION {^Actus:%s[%d]}
    {
        #VAR {_Veneratus[actus]} {%%2};
        #UNACTION {^Actus:%s[%d]};
    };
    #SEND {!agnosco};
};

#ALIAS {notitia}
{
    #NOP %1;
    #ACTION {^Your knowledge of the Power of the Unliving no longer extends into combat.$}
    {
        #VAR {_Veneratus[notitia]} {0};
        #IF {"%1" == "on"} {notitia on};
        #ELSE 
        {
            #UNACTION {^Your knowledge of the Power of the Unliving no longer extends into combat.$};
            #unACTION {^Your knowledge of the Power of the Unliving extends into combat.$};
        };
    };
    #ACTION {^Your knowledge of the Power of the Unliving extends into combat.$}
    {
        #VAR {_Veneratus[notitia]} {1};
        #IF {"%1" == "off"} {notitia off};
        #ELSE 
        {
            #UNACTION {^Your knowledge of the Power of the Unliving no longer extends into combat.$};
            #unACTION {^Your knowledge of the Power of the Unliving extends into combat.$};
        };
    };
    #SEND {!notitia};
};

#ALIAS {handler_auto_veneratus}
{
    #NOP %1;
    .debug VENERATUS {%0};
    #VAR {_Veneratus[vis][current]} {%2};
    #VAR {_Veneratus[vis][max]} {%3};
    #VAR {_Veneratus[proximus]} {%4};

    #IF {$_Veneratus[vis][current] < 20}
    {
        #SEND {!drop corpse};
        #SEND {!orare};
    };

    #IF {$my[hp][current] < @fEval{$my[hp][max]/2} && $_Veneratus[actus] > 2}
    {
        #SEND {!medicor};
    };
};

#ACTION {^{Vis:\[(\d+)\/(\d+)] Proximus:\[(.+)\%\]}$}
{
    event_raise {e_veneratus_notitia} {%2} {%3} {%4};
};
auto add {veneratus} {notitia on} {e_veneratus_notitia} {notitia off};
auto veneratus on;