#ALIAS {.rolm_combat_round}
{
    #NOP %1;
    .rolm_inc_round {$_rolm[weapon]};
    .rolm_inc_round {rounds};

    #IF {&{opponent[data][alignment]}}
    {
        #IF {$opponent[data][alignment] > 0}
        {
            .rolm_inc_round {good};
        };
        #ELSEIF {$opponent[data][alignment] < 0}
        {
            .rolm_inc_round {evil};
        };
        #ELSE
        {
            .rolm_inc_round {neutral};
        };
    };
    #IF {&{opponent[data][undead]} && "$opponent[data][undead]" == "1"}
    {
        .rolm_inc_round {undead};
    };
    #IF {&{opponent[data][size][$map[area][scaler]][avg]}}
    {
        #IF {$opponent[data][size][$map[area][scaler]][avg] <= 1000000}
        {
            .rolm_inc_round {small};
        };
        #ELSEIF {$opponent[data][size][$map[area][scaler]][avg] > 1000000 &&
                 $opponent[data][size][$map[area][scaler]][avg] <= 100000000}
        {
            .rolm_inc_round {medium};
        };
        #ELSE
        {
            .rolm_inc_round {large};
        };
    };

    .file write {$path[profile]/The Rod of Lordly Might.tin};
};
event_register {e_combat_round} {$path[equipment]/The Rod of Lordly Might.tin} {.rolm_combat_round};

#ALIAS {.rolm_inc_round}
{
    #MATH {_rolm[combat][%1]} {$_rolm[combat][%1] + 1};
    #IF {@fEval{$_rolm[combat][%1] % 200} == 0}
    {
        #IF {"%1" == "rounds"}
        {
            .rolm_check_rune {};
        };
        #ELSE
        {
            .rolm_check_rune {%1};
        };
    };
};

#ALIAS {.rolm_check_rune}
{
    #NOP %1;
    #IF {&{_rolm_delay} == 0}
    {
        #VAR {_rolm_delay} {0};
    };
    #FOREACH {*_rolm[rune][]} {_rolm_rune}
    {
        #IF {$_rolm[rune][$_rolm_rune][trained] > 22}
        {
            #CONTINUE;
        };
        #IF {"$_rolm[rune][$_rolm_rune][refers]" != "%1"}
        {
            #CONTINUE;
        };
        #LIST {_rolm[checkrune]} ADD {$_rolm_rune};
        #TICKER {_rolm} 
        {
            #IF {&{_rolm[checkrune][+1]} == 0}
            {
                #UNTICKER {_rolm};
            };
            #ELSE 
            {
                !touch $_rolm[checkrune][+1] rune;
                #LIST {_rolm[checkrune]} DELETE 1;
            };
        } {2};
    };
    #UNVAR {_rolm_rune};
};

#ALIAS {{!|}touch %w rune}
{
    #IF {"$_rolm[rune][%2][type]" == "weapon"}
    {
        .rolm_weapon_rune {%2};
    };
    #ELSE
    {
        #SWITCH {"%2"}
        {
            #CASE {"diamond"} {.rolm_diamond_rune};
            #DEFAULT {#NOP};
        };
        #SEND {%0};        
    };
};

#ALIAS {.rolm_weapon_rune}
{
    #IF {$_rolm[rune][%1][trained] >= 24}
    {
        #VAR {_rolm[weapon]} {%1};
        #SEND {!touch %1 rune};
    };
    #ELSE
    {
        touch $_rolm[rune][%1][refers] rune;
    };
};

#ALIAS {.rolm_diamond_rune}
{
    #ACTION {^Warming in your grasp, %* twinkling}
    {
        #VAR {_rolm[charges]} {@fTextToInt{%%1}};
        #UNACTION {^Warming in your grasp, %* twinkling};
    };
};

#NOP You touch the Winch rune on the artifact and hear a soft *click*
#NOP Although you are not yet attuned to that magenta rune, you are granted an
#NOP archonic vision of your bonding progression ~#=#=#=#=#=#=#=#|#=#=#=>
#ACTION {You touch the %+ rune on the artifact and hear a soft *click*\nAlthough you are not yet attuned to that %+ rune, you are granted an\narchonic vision of your bonding progression {([^\|]*)\|([^\|]*)}}
{
    #LOCAL {_rolm_progress} {};
    #FORMAT {_rolm_progress[rune]} {%l} {%1};
    #FORMAT {_rolm_progress[trained]} {%L} {%4};
    #MATH {_rolm_progress[trained]} {$_rolm_progress[trained] + 1};
    #IF {"$_rolm_progress[trained]" != "$_rolm[rune][$_rolm_progress[rune]][trained]"}
    {
        #VAR {_rolm[rune][$_rolm_progress[rune]][trained]} {$_rolm_progress[trained]};
        #IF {&{_rolm[rune][$_rolm_progress[rune]][refers]}}
        {
            .log add {The Rod of Lordly Might.log} {TRAINED $_rolm_progress[rune] rune level $_rolm_progress[trained] ($_rolm[rune][$_rolm_progress[rune]][refers] combat rounds $_rolm[combat][$_rolm[rune][$_rolm_progress[rune]][refers]])};
        };
        #ELSE
        {
            .log add {The Rod of Lordly Might.log} {TRAINED $_rolm_progress[rune] rune level $_rolm_progress[trained] (combat rounds $_rolm[combat][rounds])};
        };
        .file write {$path[profile]/The Rod of Lordly Might.tin};
    };
};


#NOP Rod Recharge Message:
#NOP Thunderclouds gather...                                                                                                                                                                                                           
#NOP                                                                                                                                                                                                                                   
#NOP A bolt of lightning strikes down from the heavens upon the holy relic!!                                                                                                                                                           
#NOP Divine energy dances up and down the artifact, welling with power renewed.


#IF {&{_rolm} == 0}
{
    #CLASS {$path[profile]/The Rod of Lordly Might.tin} ASSIGN
    {
        #VARIABLE {_rolm}
        {
            {rune}
            {
                {aegis}
                {
                    {trained} {1}
                }
                {anchor}
                {
                    {trained} {25}
                }
                {angel}
                {
                    {trained} {1}
                }
                {arachne}
                {
                    {damage} {acid}                    
                    {trained} {1}
                    {type} {weapon}
                    {refers} {viper}
                }
                {armillary}
                {
                    {trained} {1}
                }
                {aurora}
                {
                    {damage} {radiation}                    
                    {trained} {1}
                    {type} {weapon}
                    {refers} {comet}
                }
                {cairn}
                {
                    {trained} {1}
                    {refers} {undead}
                }
                {comet}
                {
                    {damage} {energy}                    
                    {trained} {1}
                    {type} {weapon}
                    {refers} {lightning}
                }
                {crown}
                {
                    {damage} {mental}                    
                    {trained} {1}
                    {type} {weapon}
                    {refers} {reaper}
                }
                {diamond}
                {
                    {trained} {25}
                }
                {dove}
                {
                    {trained} {1}
                    {refers} {evil}
                }
                {dragon}
                {
                    {trained} {1}
                    {type} {weapon}
                    {damage} {fire}
                    {refers} {star}
                }
                {eagle}
                {
                    {trained} {1}
                }
                {eye}
                {
                    {trained} {1}
                }
                {frost}
                {
                    {damage} {ice}
                    {trained} {1}
                    {type} {weapon}
                    {refers} {dragon}
                }
                {ghost}
                {
                    {trained} {1}
                    {refers} {neutral}
                }
                {gremlin}
                {
                    {trained} {1}
                }
                {heart}
                {
                    {trained} {25}
                }
                {lasso}
                {
                    {trained} {1}
                    {refers} {small}
                }
                {lightning}
                {
                    {damage} {electric}                    
                    {trained} {1}
                    {type} {weapon}
                    {refers} {frost}
                }
                {orrery}
                {
                    {trained} {1}
                }
                {owl}
                {
                    {trained} {1}
                }
                {phoenix}
                {
                    {trained} {1}
                }
                {raven}
                {
                    {trained} {1}
                    {refers} {crown}
                }
                {reaper}
                {
                    {damage} {edged}                    
                    {trained} {1}
                    {type} {weapon}
                    {refers} {arachne}
                }
                {scales}
                {
                    {trained} {1}
                    {refers} {medium}
                }
                {scroll}
                {
                    {trained} {25}
                }
                {skull}
                {
                    {trained} {1}
                    {refers} {good}
                }
                {spark}
                {
                    {trained} {25}
                }
                {star}
                {
                    {active} {1}
                    {damage} {blunt}
                    {trained} {25}
                    {type} {weapon}
                }
                {sun}
                {
                    {trained} {1}
                }
                {viper}
                {
                    {damage} {poison}                    
                    {trained} {1}
                    {type} {weapon}
                    {refers} {aurora}
                }
                {winch}
                {
                    {trained} {1}
                }
            }
            {weapon} {star}
        };
    };
};

ibrief on rod of lordly might

touch crown rune