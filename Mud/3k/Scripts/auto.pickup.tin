#ALIAS {handler_auto_pickup}
{
    #NOP %1;
    #IF {@fLocked{scan.%*}}
    {
        #RETURN;
    };
    #IF {"$room[break]" != "1"}
    {
        .debug PICKUP {%2 : %3 : %4};
        #LOCAL {_hRP[data]} {%5};
        #IF {&_hRP[data][] != 0}
        {
            #SWITCH {"$_hRP[data][pickup]"}
            {
                #CASE {"-1"}
                {
                    .debug PICKUP {Item flagged do not pick up};
                    #UNVAR {room[commandbuffer]};
                    #VAR {room[break]} {1};                
                };
                #CASE {"1"}
                {
                    .debug PICKUP {Item flagged always pick up};            
                    #VAR {room[commandbuffer]} {@fAutoGetCommand{%2}};
                };
                #CASE {""}
                {
                    .debug PICKUP {Item not specifically flagged};            
                };
                #CASE {"0"}
                {
                    .debug PICKUP {Item not specifically flagged};            
                };
                #DEFAULT 
                {
                    .debug PICKUP {Item has special command: $_hRP[data][pickup]};            
                    #LINE SUBSTITUTE VARIABLES #FORMAT {room[commandbuffer]} {$_hRP[data][pickup]} {%3};
                };
            };
        };
    };
 
    #IF {"$room[commandbuffer]" != ""}
    {
        .debug PICKUP {Command added to command queue: $room[commandbuffer]};
        .RoomAddCommand {$room[commandbuffer]} {8};
    };
    #ELSEIF {"$room[break]" != "1" && "$auto[pickup][variable]" == "full"}
    {
        .debug PICKUP {Full Pickup - get item added to command queue: $room[commandbuffer]};
        .RoomAddCommand {#SEND {!get %2}} {8};
        #IF {$auto[bag][status]}
        {
            .RoomAddCommand {#SEND {!put %2 in bag}} {9};
        };
    };

    #UNVAR {room[break]};
    #UNVAR {room[commandbuffer]};
};

auto add {pickup} {.file load {$path[mud]/Scripts/auto.pickup.tin}} {e_room_item} {#NOP %1;#CLASS {$path[mud]/Scripts/auto.pickup.tin} KILL;};