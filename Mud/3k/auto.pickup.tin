#ALIAS {autopickup}
{
  #IF {"%1" == "off" || "%1" == "stop"}
  {
    #VAR {flags[autopickup]} {0};
    #ECHO {Auto Pickup Off};
    #RETURN;
  };
  #IF {"%1" == "min"} {#VAR {flags[autopickup]} {0};};
  #IF {"%1" == "full"} {#VAR {flags[autopickup]} {1};};

  #SWITCH {"$flags[autopickup]"}
  {
    #CASE {"0"}
    {
      #VAR {flags[autopickup]} {1};
      #ECHO {Auto Pickup Minimal};
    }
    #CASE {"1"}
    {
      #VAR {flags[autopickup]} {2};
      #ECHO {Auto Pickup Full};
    };
    #CASE {"2"}
    {
      #VAR {flags[autopickup]} {1};
      #ECHO {Auto Pickup Minimal};
    };
  };

  #IF {$flags[autopickup] > 0 }
  {
    event_register e_room_item h_auto_pickup handler_room_pickup;
  };
  #ELSE
  {
    event_remove_handler h_auto_pickup;
  };
};

autopickup min;

#ALIAS {handler_room_pickup}
{
    #NOP %1;
    #IF {"$room[break]" != "1"}
    {
        .debug PICKUP {%2 : %3 : %4};
        #LOCAL {_hRP[data]} {%5};
        #IF {&_hRP[data][] != 0}
        {
            #SWITCH {"$_hRP[data][pickup]"}
            {
                #CASE {"-1"}
                {
                    .debug PICKUP {Item flagged do not pick up};
                    #UNVAR {room[commandbuffer]};
                    #VAR {room[break]} {1};                
                };
                #CASE {"1"}
                {
                    .debug PICKUP {Item flagged always pick up};            
                    #VAR {room[commandbuffer]} {@fAutoGetCommand{%2}};
                };
                #CASE {""}
                {
                    .debug PICKUP {Item not specifically flagged};            
                };
                #CASE {"0"}
                {
                    .debug PICKUP {Item not specifically flagged};            
                };
                #DEFAULT 
                {
                    .debug PICKUP {Item has special command: $_hRP[data][pickup]};            
                    #LINE SUBSTITUTE VARIABLES #FORMAT {room[commandbuffer]} {$_hRP[data][pickup]} {%3};
                };
            };
        };
    };
 
    #IF {"$room[commandbuffer]" != ""}
    {
        .debug PICKUP {Command added to command queue: $room[commandbuffer]};
        .RoomAddCommand {$room[commandbuffer]} {8};
    };
    #ELSEIF {"$room[break]" != "1" && "$flags[autopickup]" == "2"}
    {
        .debug PICKUP {Full Pickup - get item added to command queue: $room[commandbuffer]};
        .RoomAddCommand {@fAutoGetCommand{%2}} {8};
    };

    #UNVAR {room[break]};
    #UNVAR {room[commandbuffer]};
};

/* Returns all the commands to perform when getting an object */
#FUNCTION {fAutoGetCommand}
{
    #LOCAL {_fAGC} {#SEND {!get %0}};
    #IF {$auto[bag][status]}
    {
        #LOCAL {_fAGC} {${_fAGC};#SEND {!put %0 in bag};};
    };
    #RETURN {$_fAGC};
};