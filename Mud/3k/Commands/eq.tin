#ALIAS {eq}
{
    #NOP %1;
    .eqCleanup;
    .eqLoadFilelist;

#NOP    #ACTION {{^([^:]{3,13}):\s+(.*)$}};
    #ACTION {{^(?:(.*):\s{1,11}|\s{14})(\w.*)$}}
	{
		#IF {"%%2" != "Resistances"}
		{
            #IF {"%%2" != "            "}
            {
                #VAR {_eq[type]} {%%2};
            };
			#IF {"%%3" != "none"} 
			{
                #VAR {my[eq][%%3][type]} {$_eq[type]};
                .eqLoadScript {%%3};
			};
		};
        #ELSE
        {
            #UNVAR {_eq};        
            #UNACTION {{^(?:(.*):\s{1,11}|\s{14})(\w.*)$}};
        };
	};

	#SEND {!eq %0};
};

#ALIAS {.eqCleanup}
{
    #UNVAR {my[eq]};
    #KILL CLASS {$path[equipment]/%*};
    event_remove_handler {$path[equipment]/%*};
};

#ALIAS {.eqLoadFilelist}
{
    #VAR {_eq[filelist]} {@fFileList{$path[equipment]}};

    #FOREACH {$_eq[filelist][]} {_eq[item]}
    {
        #VAR {_eq[path]} {$_eq[item]};
        #REPLACE {_eq[item]} {$path[equipment]/} {};
        #REPLACE {_eq[item]} {.tin} {};
        #FOREACH {{(}{)}{<}{>}{?}{!}{*}{%}{"}} {_eq[char]}
        {
          #REPLACE {_eq[item]} {@fAsciiHex{{$_eq[char]}{x}}} {$_eq[char]};
        }        
        #VAR {_eq[filetable][$_eq[item]]} {$_eq[path]};
    };
};

#ALIAS {.eqLoadScript}
{
    #NOP %1;
    #FOREACH {*_eq[filetable][]} {_eq[match]}
    {
        #REGEXP {%1} {$_eq[match]}
        {
            .file load {$_eq[filetable][$_eq[match]]};
            event_raise {e_equipment_loaded} {%1} {$_eq[filetable][$_eq[match]]};
        };
    };
};

#VAR {_equipment} {};

eq;