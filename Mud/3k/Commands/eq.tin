#ALIAS {eq}
{
    #NOP %1;
    #VAR {_eq[filelist]} {@fFileList{$path[equipment]}};

    #FOREACH {$_eq[filelist][]} {_eq[item]}
    {
        #VAR {_eq[path]} {$_eq[item]};
        #REPLACE {_eq[item]} {$path[equipment]/} {};
        #REPLACE {_eq[item]} {.tin} {};
        #FOREACH {{(}{)}{<}{>}{?}{!}{*}{%}{"}} {_eq[char]}
        {
          #REPLACE {_eq[item]} {@fAsciiHex{{$_eq[char]}{x}}} {$_eq[char]};
        }        
        #VAR {_eq[filetable][$_eq[item]]} {$_eq[path]};
    };

#NOP    #ACTION {{^([^:]{3,13}):\s+(.*)$}};
    #ACTION {{^(.{12})\s{2}(.*)$}}
	{
		#IF {"%%2" != "Resistances:"}
		{
            #IF {"%%2" != "            "}
            {
                #VAR {_eq[type]} {%%2};
            };
			#IF {"%%3" != "none"} 
			{
                #VAR {my[eq][%%3][type]} {$_eq[type]};
                .eqLoadScript {%%3};
			};
		};
        #ELSE
        {
            #UNVAR {_eq};
            #UNACTION {{^(.{12})\s{2}(.*)$}};
        };
	};

    #UNVAR {my[eq]};
    #KILL CLASS {$path[equipment]/%*};
    event_remove_handler {$path[equipment]/%*};
	#SEND {!eq %0};
};

#ALIAS {.eqLoadScript}
{
    #NOP %1;
    #FOREACH {*_eq[filetable][]} {_eq[match]}
    {
        #REGEXP {%1} {$_eq[match]}
        {
            .file load {$_eq[filetable][$_eq[match]]};
        };
    };
};

eq;