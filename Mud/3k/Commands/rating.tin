#ALIAS {rating}
{
  #CLASS {cRating} {OPEN};
  #ACTION {{^\S*AREA NAME\S*:\s(.*)\s\S*\[\W*(\w+)\S*\]\S*$}}
  {
    #IF {"$room[location][name]" != "%%2" || "$room[location][wiz]" != "%%3"}
    {
      #VAR {map[area][name]} {%%2};
      #VAR {map[area][wiz]} {%%3};
      #VAR {room[location][name]} {%%2};
      #VAR {room[location][wiz]} {%%3};
    };
    event_raise {e_command_rating} {%%2} {%%3};
    event_oneshot {e_prompt} {h_command_rating} {#NOP %%%1;#CLASS {cRating} {KILL};};
  } {1};
  #ACTION {{^\S*AREA RATING\S* -> .* \S*\[\W*(\w+)\S*\]\S*$}}
  {
    #IF {"$room[location][name]" != "%%2"}
    {
      #VAR {map[area][name]} {%%2};
      #VAR {map[area][wiz]} {};
      #VAR {room[location][name]} {%%2};
      #VAR {room[location][wiz]} {};      
    };
    event_raise {e_command_rating} {%%2} {};
    event_oneshot {e_prompt} {h_command_rating} {#NOP %%%1;#CLASS {cRating} {KILL};};
  } {1};
  #ACTION {^This area has no rating.  Please use the idea command to request one.}
  {
    #IF {"$map[area][name]" != ""}
    {
      #VAR {map[area][name]} {};
      #VAR {room[location][name]} {};
    };
    event_raise {e_command_rating} {} {};
    event_oneshot {e_prompt} {h_command_rating} {#NOP %%%1;#CLASS {cRating} {KILL};};
    where;
  } {1};    
  #CLASS {cRating} {CLOSE}; 
  #SEND {!rating};
};