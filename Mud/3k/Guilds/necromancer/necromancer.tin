#NOP ************* Necromancer Guild *********;
#VAR {_guild[minions]}
{
    {Skeleton}{} {Zombie}{} {Ghoul}{} {Valpur}{}
    {Ghast}{} {Manshi}{} {Bolwyn}{} {Greesh}{}
    {Wight}{} {Heucuva}{} {Tholen}{} {Bayen}{}
    {Quarin}{} {Mohrg}{} {Grimare}{} {Valithyn}{}
};

/* Teleport to Guild */
#ACTION {^Visions of the guild appear first in your mind, then reality...}
{
    #NOP %1;
    .mapLoad {Pinnacle/Fantasy/Necromancer Guild};
    #map goto {20};
};

/* Call your familiar into combat automatically when you "kill" */
#ALIAS {handler_auto_call}
{
    #NOP %1;
    #SEND {!call};
};
auto {add} {call} {} {e_combat} {};

/* Corrode your opponent automatically when you "kill" */
#ALIAS {handler_auto_corrode}
{
    #NOP %1;
    #IF {$my[necromancer][power][corrode] > 0 &&
         $my[gp1][current] > 50 &&
         $my[sp][current] > 50}
    {
        #SEND {!corrode};
    };
};
auto {add} {corrode} {} {e_combat} {};

/* Contribute half of coins picked up to the guild, between 10 and 1000 */
#ALIAS {handler_auto_contribute}
{
    #NOP %1;
    #IF {"%1" == "e_pickup"}
    {
        #REGEXP {%2} {coins (%d)}
        {
            #IF {@fEval{&1/2} > 9}
            {
                #SEND {!contribute @fMin{@fEval{&1/2};1000}};
            };
        };
    };
    #ELSEIF {$my[necromancer][worth] < 125}
    {
        #SEND {!contribute @fMax{@fEval{(125-$my[necromancer][worth])*5};10}};
    };
};
auto {add} {contribute} {} {e_pickup;e_combat_round} {};

/* SPFuse when below 75% SP (it refills by itself when SP is full) */
#ALIAS {handler_auto_spfuse}
{
    #NOP %1;
    #VAR {_necSP[Diff]} {@fEval{($my[sp][max]+0.00-($my[sp][max]-$my[sp][current]))/$my[sp][max]}};
    #IF {$_necSP[Diff] < 0.75 && $my[necromancer][spfuse][current] > 0} 
    {
        #SEND {!spfuse $my[necromancer][spfuse][current]};
    };
};
auto {add} {spfuse} {} {e_combat_round} {};

/* Automatically recast protection (if it's memorised and there's enough NP */
#ALIAS {handler_auto_protection}
{
    #NOP %1;
    #IF {"$my[necromancer][protection]" != "ON" && 
         $my[necromancer][power][protection] > 0 &&
         $my[gp1][current] > 55}
    {
        #SEND {!protection};
    };
};
auto {add} {protection} {} {e_combat_round} {};

/* Automatically invoke veil if fighting an undead */
#ALIAS {handler_auto_veil}
{
    #NOP %1;
#NOP Veil now works for non-undead (just less effective)
#NOP     #IF {"$_npc[%2][undead]" == "1" && "$my[necromancer][veil]" != "ON"}
#NOP     {
#NOP         #SEND {!veil invoke};
#NOP         #VAR {my[necromancer][veil]} {ON};
#NOP     };
#NOP     #ELSEIF {"$_npc[%2][undead]" != "1" && "$my[necromancer][veil]" == "ON"}
#NOP     {
#NOP         #SEND {!veil disperse};
#NOP         #VAR {my[necromancer][veil]} {OFF};
#NOP     };

    #IF {"$my[necromancer][veil]" != "ON"}
    {
        #SEND {!veil invoke};
        #VAR {my[necromancer][veil]} {ON};
    };
};
auto {add} {veil} {} {e_combat} {};

/* Automatically fire glamors */
#ALIAS {handler_auto_glamor}
{
    #NOP %1;
    #IF {(@fEval{$my[necromancer][glamors][max]-$my[necromancer][glamors][current]} < 
          @fEval{$my[gp2][current]/(100/$my[necromancer][glamors][max])}) &&
          "$my[necromancer][glamors][active]" == ""}
    {
        #IF {"$my[necromancer][glamors][command]" != ""}
        {
            #SEND {!$my[necromancer][glamors][command]};
        };
        #ELSE
        {
            #ECHO {AUTO GLAMOR WARNING: my[necromancer][glamors][command not set};
        };
    };
};
auto {add} {glamor} {} {e_combat_round} {};

#FUNCTION {fMinionPresent}
{
	#LOCAL {_fMPi} {};
	#LOCAL {_fMPt} {};
	#FORMAT {_fMPt} {%n} {$my[necromancer][undead][type]};
	#FOREACH {$room[contents][npc]} {_fMPi}
	{
		#IF {"$room[contents][npc][$_fMPi][id]" == ""}
		{
			#CONTINUE;
		};
		#ELSEIF {"$room[contents][npc][$_fMPi][id]" == "$my[necromancer][undead][name] the $_fMPt"}
		{
			#RETURN 1;
		};
		#ELSEIF {&_guild[minions][$room[contents][npc][$_fMPi][id]] > 0}
		{
			#RETURN 1;
		};
	};
	#RETURN 0;
};

#ACTION {^A {bayen|bolwyn|ghast|ghoul|greesh|grimare|heucuva|manshi|mohrg|quarin|skeleton|tholen|valithyn|valpur|wight|zombie} {trudges in|drifts in on a ghostly vapor}.}
{
	#LOCAL {_aMinEnter} {%1};
	#FORMAT {_aMinEnter} {%n} {$_aMinEnter};
	#LIST {room[contents][npc]} {ADD} {{{id}{$_aMinEnter}}};
	handler_auto_corpse;
};