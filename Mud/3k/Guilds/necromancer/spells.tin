#ALIAS {handler_necspells_combat_end}
{
    #NOP %1;
    #ACTION {^%s{Edged|Crushing|Cold|Flame|Psionic|Lightning|Corrosion|Virulence|Power|Light}-\(%s%d\)-(%s%d\%\)}
    {
        #FORMAT {_necSpell} {%l} {%%2};
        #LOCAL {_stdSpell} {@fStandardDamage{%%2}};
        #VAR {my[necromancer][spells][%%4][$_necSpell]} {%%6};
        #IF {"%%6" != "0"}
        {
            #IF {&_npc[$opponent[regexp]][vulnerabilities][$_stdSpell] == 0} 
            {
                #VAR {_npc[$opponent[regexp]][vulnerabilities][$_stdSpell]} {0};
            };
            #VAR {_npc[$opponent[regexp]][vulnerabilities][$_stdSpell]} {@fMax{{%%6}{$_npc[$opponent[regexp]][vulnerabilities][$_stdSpell]}}};
			.scanRoomNPCSave {$opponent[regexp]};	            
        };
        #UNVAR {_necSpell};
    };
    #ACTION {^Your spells minimum is set to}
    {
        #UNACTION {^Your spells minimum is set to};
        #UNACTION {^%s{Edged|Crushing|Cold|Flame|Psionic|Lightning|Corrosion|Virulence|Power|Light}-\(%s%d\)-(%s%d\%\)};
    };
    #UNVAR {my[necromancer][spells]};
    #SEND {!spells};
};
event_register e_killed_opponent spells.tin handler_necspells_combat_end; 

#ALIAS {handler_necspells_combat_start}
{
    #NOP %1;
    #LOCAL {_necSpells} {};
    #SEND {!spells clear};
    #LOCAL {_necSpells[max]} {0};
    #FOREACH {*my[necromancer][spells][@fMax{*my[necromancer][spells][]}][]} {s}
    {
        #LOCAL {_stdSpell} {@fStandardDamage{$s}};        
        #IF {&opponent[data][vulnerabilities][$_stdSpell] == 0}
        {
            #LOCAL {_necSpells[max]} {101};            
            #LOCAL {_necSpells[invoke]} {$s};
        };
        #ELSEIF {$opponent[data][vulnerabilities][$_stdSpell] >= $_necSpells[max]}
        {
            #LOCAL {_necSpells[max]} {$opponent[data][vulnerabilities][$s]};
            #LOCAL {_necSpells[invoke]} {$s};
        };
    };
    #IF {"$_necSpells[invoke]" = ""}
    {
        #LOCAL {_necSpells[invoke]} {*my[necromancer][spells][@fMax{*my[necromancer][spells][]}][+1]};
    };    
    #SEND {!comtrance invoke $_necSpells[invoke]};
};
event_register e_combat spells.tin handler_necspells_combat_start; 